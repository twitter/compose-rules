buildscript {
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath libs.kotlin.gradlePlugin
    }
}

plugins {
    alias(libs.plugins.spotless)
    alias(libs.plugins.gradle.maven.publish)
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

subprojects {
    apply plugin: "com.diffplug.spotless"
    spotless {
        kotlin {
            target "**/*.kt"
            // ktlint 0.47.0 currently not supported - https://github.com/diffplug/spotless/pull/1303
            // ktlint(libs.versions.ktlint.get())
            ktlint("0.46.1")
            licenseHeaderFile rootProject.file('spotless/copyright.txt')
        }
        groovyGradle {
            target "**/*.gradle"
            greclipse().configFile(rootProject.file("spotless/greclipse.properties"))
            licenseHeaderFile rootProject.file('spotless/copyright.txt'), '(buildscript|apply|import|plugins)'
        }
    }
    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach { compile ->
        kotlinOptions {
            // Treat all Kotlin warnings as errors
            allWarningsAsErrors = true
            // Set JVM target to 11
            jvmTarget = JavaVersion.VERSION_11
            // Allow use of @OptIn
            freeCompilerArgs += "-opt-in=kotlin.RequiresOptIn"
            // Enable default methods in interfaces
            freeCompilerArgs += "-Xjvm-default=all"
        }
    }
}
